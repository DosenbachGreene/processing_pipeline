#!/bin/csh -f
echo "\n START: CreateRibbon"

source $1 #Subject-specific instructions
source $2 #Project instructions

set wb_command = `which wb_command`
set workbenchdir = `dirname $wb_command`

if ( $#mprdirs > 1 ) then
	set mpr = ${structid}_T1w_debias_avg
else
	set mpr = ${structid}_T1w_1_debias
endif

if ( $#t2wdirs > 1 ) then
	set t2wimg = ${structid}_T2w_debias_avg
else
	set t2wimg = ${structid}_T2w_1_debias
endif

if ( ! $?nlalign ) set nlalign = 0
if ( $nlalign ) then				# nonlinear atlas alignment will be computed
	set tres = MNI152_T1;			# string used to construct the postmat filename
	set outspacestr = "nl_"
	set fnwarp = ${studydir}/${patid}/T1/atlas/fnirt/${mpr}_to_MNI152_T1_2mm_fnirt_coeff	# warp file generated by fnirt
else
	set tres = 711-2B_111;
	set outspacestr = ""
endif
if ( ! $?outspace_flag ) then 
	echo "The variable outspace_flag must be defined."
	exit -1
endif

# Outspace for structural data is set as 111 regardless of case
switch ( $outspace_flag )
	case "333":
		set outspace = $REFDIR/711-2B_111
		set atlasdir = 711-2B
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_711-2B_111.mat; breaksw;
	case "222":
		set outspace = $REFDIR/711-2B_222
		set atlasdir = 711-2B
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_711-2B_111.mat; breaksw;
	case "222AT":
		set outspace = $REFDIR/711-2B_222AT
		set atlasdir = 711-2B
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_711-2B_111.mat; breaksw;
	case "111":
		set outspace = $REFDIR/711-2B_111
		set atlasdir = 711-2B
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_711-2B_111.mat; breaksw;
	case "mni3mm":
		set outspace = $REFDIR/MNI152/MNI152_T1_3mm
		set atlasdir = MNI
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_MNI152_T1.mat; breaksw;
	case "mni2mm":
		set outspace = $REFDIR/MNI152/MNI152_T1_2mm
		set atlasdir = MNI
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_MNI152_T1.mat; breaksw;
	case "mni1mm":
		set outspace = $REFDIR/MNI152/MNI152_T1_1mm
		set atlasdir = MNI
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_MNI152_T1.mat; breaksw;
	default:
		set outspace = `echo $outspace | sed -E 's/\.4dfp(\.img){0,1}$//'`
endsw

if ( $nlalign ) then
	set atlasdir = ${atlasdir}_nonlinear
else
	set atlasdir = ${atlasdir}
endif

set outspacestr = ${outspacestr}${outspace:t}	# e.g., "nl_711-2B_333"
set freelabels = ${DATA_DIR}/FreeSurferAllLut.txt

set LeftGreyRibbonValue="3"
set LeftWhiteMaskValue="2"
set RightGreyRibbonValue="42"
set RightWhiteMaskValue="41"

#Create ribbon in atlas space
set atlassurfdir = ${PostFSdir}/${structid}/${atlasdir}
set T1image = ${studydir}/${patid}/T1/atlas/${mpr}_on_${outspacestr}.nii.gz 
set origdir = ${atlassurfdir}/Native
set outputdir = ${atlassurfdir}/Native/Ribbon

foreach hem ( L R )
  if  ( $hem == "L" ) then
    set GreyRibbonValue="$LeftGreyRibbonValue"
    set WhiteMaskValue="$LeftWhiteMaskValue"
  endif

  if  ( $hem == "R" ) then
    set GreyRibbonValue="$RightGreyRibbonValue"
    set WhiteMaskValue="$RightWhiteMaskValue"
  endif
   

  echo "${outputdir}"
  mkdir -p "${outputdir}"
  echo ${workbenchdir}/wb_command -create-signed-distance-volume ${origdir}/${structid}.${hem}.white.native.surf.gii ${T1image} ${outputdir}/${structid}.${hem}.white.native.nii.gz 
  ${workbenchdir}/wb_command -create-signed-distance-volume ${origdir}/${structid}.${hem}.white.native.surf.gii ${T1image} ${outputdir}/${structid}.${hem}.white.native.nii.gz 
  ${workbenchdir}/wb_command -create-signed-distance-volume ${origdir}/${structid}.${hem}.pial.native.surf.gii ${T1image} ${outputdir}/${structid}.${hem}.pial.native.nii.gz 

  fslmaths ${outputdir}/${structid}.${hem}.white.native.nii.gz -thr 0 -bin -mul 255 ${outputdir}/${structid}.${hem}.white_thr0.native.nii.gz
  fslmaths ${outputdir}/${structid}.${hem}.white_thr0.native.nii.gz -bin ${outputdir}/${structid}.${hem}.white_thr0.native.nii.gz
  fslmaths ${outputdir}/${structid}.${hem}.pial.native.nii.gz -uthr 0 -abs -bin -mul 255 ${outputdir}/${structid}.${hem}.pial_uthr0.native.nii.gz
  fslmaths ${outputdir}/${structid}.${hem}.pial_uthr0.native.nii.gz -bin ${outputdir}/${structid}.${hem}.pial_uthr0.native.nii.gz
  fslmaths ${outputdir}/${structid}.${hem}.pial_uthr0.native.nii.gz -mas ${outputdir}/${structid}.${hem}.white_thr0.native.nii.gz -mul 255 ${outputdir}/${structid}.${hem}.ribbon_${outspacestr}.nii.gz
  fslmaths ${outputdir}/${structid}.${hem}.ribbon_${outspacestr}.nii.gz -bin -mul $GreyRibbonValue ${outputdir}/${structid}.${hem}.ribbon_${outspacestr}.nii.gz
 
  rm -f ${outputdir}/${structid}.${hem}.white.native.nii.gz ${outputdir}/${structid}.${hem}.white_thr0.native.nii.gz ${outputdir}/${structid}.${hem}.pial.native.nii.gz ${outputdir}/${structid}.${hem}.pial_uthr0.native.nii.gz ${outputdir}/${structid}.${hem}.white_uthr0.native.nii.gz ${outputdir}/${structid}.${hem}.white_mask.native.nii.gz
end

fslmaths ${outputdir}/${structid}.L.ribbon_${outspacestr}.nii.gz -add ${outputdir}/${structid}.R.ribbon_${outspacestr}.nii.gz ${outputdir}/ribbon_${outspacestr}.nii.gz
${workbenchdir}/wb_command -volume-label-import ${outputdir}/ribbon_${outspacestr}.nii.gz ${freelabels} ${outputdir}/ribbon_${outspacestr}.nii.gz -discard-others -unlabeled-value 0

# Create ribbon in native space
set nativesurfdir = ${PostFSdir}/${structid}/NativeVol
set T1image = ${studydir}/${patid}/T1/${mpr}.nii.gz 
set origdir = ${nativesurfdir}/Native
set outputdir = ${nativesurfdir}/Native/Ribbon

foreach hem ( L R )
  if  ( $hem == "L" ) then
    set GreyRibbonValue="$LeftGreyRibbonValue"
    set WhiteMaskValue="$LeftWhiteMaskValue"
  endif

  if  ( $hem == "R" ) then
    set GreyRibbonValue="$RightGreyRibbonValue"
    set WhiteMaskValue="$RightWhiteMaskValue"
  endif
   

  echo "${outputdir}"
  mkdir -p "${outputdir}"
  echo ${workbenchdir}/wb_command -create-signed-distance-volume ${origdir}/${structid}.${hem}.white.native.surf.gii ${T1image} ${outputdir}/${structid}.${hem}.white.native.nii.gz 
  ${workbenchdir}/wb_command -create-signed-distance-volume ${origdir}/${structid}.${hem}.white.native.surf.gii ${T1image} ${outputdir}/${structid}.${hem}.white.native.nii.gz 
  ${workbenchdir}/wb_command -create-signed-distance-volume ${origdir}/${structid}.${hem}.pial.native.surf.gii ${T1image} ${outputdir}/${structid}.${hem}.pial.native.nii.gz 

  fslmaths ${outputdir}/${structid}.${hem}.white.native.nii.gz -thr 0 -bin -mul 255 ${outputdir}/${structid}.${hem}.white_thr0.native.nii.gz
  fslmaths ${outputdir}/${structid}.${hem}.white_thr0.native.nii.gz -bin ${outputdir}/${structid}.${hem}.white_thr0.native.nii.gz
  fslmaths ${outputdir}/${structid}.${hem}.pial.native.nii.gz -uthr 0 -abs -bin -mul 255 ${outputdir}/${structid}.${hem}.pial_uthr0.native.nii.gz
  fslmaths ${outputdir}/${structid}.${hem}.pial_uthr0.native.nii.gz -bin ${outputdir}/${structid}.${hem}.pial_uthr0.native.nii.gz
  fslmaths ${outputdir}/${structid}.${hem}.pial_uthr0.native.nii.gz -mas ${outputdir}/${structid}.${hem}.white_thr0.native.nii.gz -mul 255 ${outputdir}/${structid}.${hem}.ribbon_native.nii.gz
  fslmaths ${outputdir}/${structid}.${hem}.ribbon_native.nii.gz -bin -mul $GreyRibbonValue ${outputdir}/${structid}.${hem}.ribbon_native.nii.gz
 
  rm -f ${outputdir}/${structid}.${hem}.white.native.nii.gz ${outputdir}/${structid}.${hem}.white_thr0.native.nii.gz ${outputdir}/${structid}.${hem}.pial.native.nii.gz ${outputdir}/${structid}.${hem}.pial_uthr0.native.nii.gz ${outputdir}/${structid}.${hem}.white_uthr0.native.nii.gz ${outputdir}/${structid}.${hem}.white_mask.native.nii.gz
end

fslmaths ${outputdir}/${structid}.L.ribbon_native.nii.gz -add ${outputdir}/${structid}.R.ribbon_native.nii.gz ${outputdir}/ribbon_native.nii.gz
${workbenchdir}/wb_command -volume-label-import ${outputdir}/ribbon_native.nii.gz ${freelabels} ${outputdir}/ribbon_native.nii.gz -discard-others -unlabeled-value 0
echo -e "\n END: CreateRibbon"

