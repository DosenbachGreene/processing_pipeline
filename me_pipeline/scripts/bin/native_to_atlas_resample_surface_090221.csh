#!/bin/csh

source $1 #Subject-specific instructions
source $2 #Project instructions

set wb_command = `which wb_command`
set workbenchdir = `dirname $wb_command`

if ( $#mprdirs > 1 ) then
	set mpr = ${structid}_T1w_debias_avg
else
	set mpr = ${structid}_T1w_1_debias
endif

if ( $#t2wdirs > 1 ) then
	set t2wimg = ${structid}_T2w_debias_avg
else
	set t2wimg = ${structid}_T2w_1_debias
endif

if ( ! $?nlalign ) set nlalign = 0
if ( $nlalign ) then				# nonlinear atlas alignment will be computed
	set tres = MNI152_T1;			# string used to construct the postmat filename
	set outspacestr = "nl_"
	set fnwarp = ${studydir}/${patid}/T1/atlas/fnirt/${mpr}_to_MNI152_T1_2mm_fnirt_coeff	# warp file generated by fnirt
else
	set tres = 711-2B_111;
	set outspacestr = ""
endif
if ( ! $?outspace_flag ) then 
	echo "The variable outspace_flag must be defined."
	exit -1
endif

# Outspace for structural data is set as 111 regardless of case
switch ( $outspace_flag )
	case "333":
		set outspace = $REFDIR/711-2B_111
		set atlasdir = 711-2B
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_711-2B_111.mat; breaksw;
	case "222":
		set outspace = $REFDIR/711-2B_111
		set atlasdir = 711-2B
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_711-2B_111.mat; breaksw;
	case "222AT":
		set outspace = $REFDIR/711-2B_111
		set atlasdir = 711-2B
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_711-2B_111.mat; breaksw;
	case "111":
		set outspace = $REFDIR/711-2B_111
		set atlasdir = 711-2B
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_711-2B_111.mat; breaksw;
	case "mni3mm":
		set outspace = $REFDIR/MNI152/MNI152_T1_1mm
		set atlasdir = MNI
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_MNI152_T1.mat; breaksw;
	case "mni2mm":
		set outspace = $REFDIR/MNI152/MNI152_T1_1mm
		set atlasdir = MNI
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_MNI152_T1.mat; breaksw;
	case "mni1mm":
		set outspace = $REFDIR/MNI152/MNI152_T1_1mm
		set atlasdir = MNI
		set postmat =  $REFDIR/FSLTransforms/${tres}_to_MNI152_T1.mat; breaksw;
	case "D991mm":
		set outspace = /data/nil-bluearc/GMT/Evan/Atlases/D99_v2.0_dist/D99_template_1mm
		set atlasdir = D99
        set refmask = /data/nil-bluearc/GMT/Evan/Atlases/D99_v2.0_dist/D99_template_mask
		set postmat =  ignore; breaksw;
	default:
		set outspace = `echo $outspace | sed -E 's/\.4dfp(\.img){0,1}$//'`
endsw
set outspacestr = ${outspacestr}${outspace:t}	# e.g., "nl_711-2B_333"

set T1dir = ${studydir}/${patid}/T1
set T1_Nativevol = ${mpr}
set T2dir = ${studydir}/${patid}/T2
set T2_Nativevol = ${t2wimg}

set nativedir = ${PostFSdir}/${structid}/NativeVol
set resampledir = ${PostFSdir}/${structid}/${atlasdir}

if ( -e ${resampledir} ) rm -rf ${resampledir}
if ( ! -d ${resampledir} ) mkdir ${resampledir}
#Copy native surfaces over to start
pushd ${nativedir}
cp -r * ${resampledir}
if ( -e ${T2dir}/${T2_Nativevol}.nii.gz ) cp ${T2dir}/${T2_Nativevol}.nii.gz .
popd

pushd ${resampledir}
cp ${T2dir}/${T2_Nativevol}.nii.gz .
convert_xfm -omat ${mpr}_to_${outspace:t}.mat -concat $postmat ${T1dir}/atlas/${mpr}_to_${target:t}_111.mat
flirt -ref $outspace -in ${mpr} -applyxfm -init ${mpr}_to_${outspace:t}.mat -out ${mpr}_on_${outspacestr} || exit $status 
flirt -ref $outspace -in wmparc -applyxfm -init ${mpr}_to_${outspace:t}.mat -interp nearestneighbour -out \
	${structid}_wmparc_on_${outspacestr} || exit $status
flirt -ref $outspace -in aparc+aseg -applyxfm -init ${mpr}_to_${outspace:t}.mat -interp nearestneighbour -out \
	${structid}_aparc+aseg_on_${outspacestr} || exit $status


convert_xfm -omat ${t2wimg}_to_${outspace:t}.mat -concat ${mpr}_to_${outspace:t}.mat ${T1dir}/atlas/${t2wimg}_to_${mpr}.mat
flirt -ref $outspace -in ${t2wimg} -applyxfm -init ${t2wimg}_to_${outspace:t}.mat -out ${t2wimg}_on_${outspacestr} || exit $status

set T1_Atlasvol = ${mpr}_on_${outspacestr}
set T2_Atlasvol = ${t2wimg}_on_${outspacestr}

#gunzip ${resampledir}/${T1_Nativevol}.nii.gz
#gunzip ${resampledir}/${T1_Atlasvol}.nii.gz

#Set mat file
set matfile = ${mpr}_to_${outspace:t}.mat

set matrix = `cat ${matfile}`
set surfaces = ( midthickness white pial inflated very_inflated )

#Fsaverage 164k
foreach surface ( $surfaces )
	${workbenchdir}/wb_command -surface-apply-affine ${structid}.L.${surface}.164k_fs_LR.surf.gii ${resampledir}/${matfile} ${structid}.L.${surface}.164k_fs_LR.surf.gii -flirt ${resampledir}/${T1_Nativevol}.nii.gz ${resampledir}/${T1_Atlasvol}.nii.gz
	${workbenchdir}/wb_command -surface-apply-affine ${structid}.R.${surface}.164k_fs_LR.surf.gii ${resampledir}/${matfile} ${structid}.R.${surface}.164k_fs_LR.surf.gii -flirt ${resampledir}/${T1_Nativevol}.nii.gz ${resampledir}/${T1_Atlasvol}.nii.gz
	
end
${workbenchdir}/wb_command -add-to-spec-file ${structid}.164k_fs_LR.wb.spec OTHER ${resampledir}/${T1_Atlasvol}.nii.gz
${workbenchdir}/wb_command -add-to-spec-file ${structid}.164k_fs_LR.wb.spec OTHER ${resampledir}/${T2_Atlasvol}.nii.gz

#Native surface
pushd Native
foreach surface ( $surfaces )
	${workbenchdir}/wb_command -surface-apply-affine ${structid}.L.${surface}.native.surf.gii ${resampledir}/${matfile} ${structid}.L.${surface}.native.surf.gii -flirt ${resampledir}/${T1_Nativevol}.nii.gz ${resampledir}/${T1_Atlasvol}.nii.gz
	${workbenchdir}/wb_command -surface-apply-affine ${structid}.R.${surface}.native.surf.gii ${resampledir}/${matfile} ${structid}.R.${surface}.native.surf.gii -flirt ${resampledir}/${T1_Nativevol}.nii.gz ${resampledir}/${T1_Atlasvol}.nii.gz
end
${workbenchdir}/wb_command -add-to-spec-file ${structid}.native.wb.spec OTHER ${resampledir}/${T1_Atlasvol}.nii.gz
${workbenchdir}/wb_command -add-to-spec-file ${structid}.native.wb.spec OTHER ${resampledir}/${T2_Atlasvol}.nii.gz
popd

#Fsaverage32k
pushd fsaverage_LR32k
foreach surface ( $surfaces )
	${workbenchdir}/wb_command -surface-apply-affine ${structid}.L.${surface}.32k_fs_LR.surf.gii ${resampledir}/${matfile} ${structid}.L.${surface}.32k_fs_LR.surf.gii -flirt ${resampledir}/${T1_Nativevol}.nii.gz ${resampledir}/${T1_Atlasvol}.nii.gz
	${workbenchdir}/wb_command -surface-apply-affine ${structid}.R.${surface}.32k_fs_LR.surf.gii ${resampledir}/${matfile} ${structid}.R.${surface}.32k_fs_LR.surf.gii -flirt ${resampledir}/${T1_Nativevol}.nii.gz ${resampledir}/${T1_Atlasvol}.nii.gz	
end
${workbenchdir}/wb_command -add-to-spec-file ${structid}.32k_fs_LR.wb.spec OTHER ${resampledir}/${T1_Atlasvol}.nii.gz
${workbenchdir}/wb_command -add-to-spec-file ${structid}.32k_fs_LR.wb.spec OTHER ${resampledir}/${T2_Atlasvol}.nii.gz
popd

#gzip ${resampledir}/${T1_Nativevol}.nii
#gzip ${resampledir}/${T1_Atlasvol}.nii

